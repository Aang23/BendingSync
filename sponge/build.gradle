buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
	maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

group = "com.aang23.bendingsync"
archivesBaseName = "bendingsync-sponge"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2838"
    runDir = "run"
    mappings = "stable_39"
}

repositories {
	jcenter()
    maven { url = "https://files.minecraftforge.net/maven" }
    maven { url = "https://repo.spongepowered.org/maven" }
    maven { url = "http://repo.amuzil.com/repository/public" }
    maven { url = "http://repo.drnaylor.co.uk/artifactory/list/minecraft" }
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    compile 'org.spongepowered:spongeapi:7.1.0'
    compile 'me.lucko.luckperms:luckperms-api:4.3'
    compile "com.crowsofwar.avatar:avatarmod:1.5.15-alpha"
    compile ("me.ryanhamshire:griefprevention:1.12.2-4.3.0.630:api") {exclude group: 'org.bstats', module: 'bstats-sponge'}
    compile "io.github.nucleuspowered:nucleus-api:1.12.0-SNAPSHOT-S7.1"

    shade group: 'redis.clients', name: 'jedis', version: '3.1.0-m3'
    shade 'mysql:mysql-connector-java:8.0.12'
    shade group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1'

    // TODO find a maven repo for those or use mine
    compile files('libs/dss.jar')
    compile files('libs/dss-api.jar')
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

uploadArchives {
    repositories {
       mavenDeployer {
             repository(url: "http://www.altillimity.com/nexus/content/repositories/releases") {
             authentication(userName: mavenUser, password: mavenPassword)
	    }
             pom.version = "$version-1.12.2"
             pom.artifactId = "bendingsync-sponge"
             pom.groupId = "com.aang23.bendingsync"
       }
    }
}
